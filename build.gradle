plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java'
}

group = 'net.motimaa'
version = '0.1-SNAPSHOT'

tasks.withType(JavaCompile).configureEach {
    options.release = 16
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        name 'papermc-repo'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
    maven {
        name "EngineHub"
        url "https://maven.enginehub.org/repo/"
    }
    maven {
        name "Citizens2"
        url "https://repo.citizensnpcs.co/"
    }
    maven {
        name "PlaceholderAPI"
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'net.luckperms:api:5.3'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.6'
    compileOnly 'net.citizensnpcs:citizens-main:2.0.28-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.10'

    // Dependency injection
    implementation 'com.google.dagger:dagger:2.40'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.40'

    // Tools
    implementation 'com.google.code.gson:gson:2.8.9'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build.dependsOn tasks.shadowJar

shadowJar {
    relocate 'com.google.gson', 'lib.com.google.gson'
    relocate 'dagger', 'lib.dagger'
    relocate 'javax.inject', 'lib.javax.inject'

    destinationDirectory.set(file("$rootDir/builds/"))
    archiveBaseName.set('SkyblockCore')
    archiveClassifier.set('')
}
