plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java'
}

group = 'net.motimaa'
version = '1.0.0-SNAPSHOT'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        name 'papermc-repo'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
    maven {
        name "EngineHub"
        url "https://maven.enginehub.org/repo/"
    }
    maven {
        name "Citizens2"
        url "https://repo.citizensnpcs.co/"
    }
    maven {
        name "PlaceholderAPI"
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name "ProtocolLib"
        url "https://repo.dmulloy2.net/repository/public/"
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.6'
    compileOnly 'net.citizensnpcs:citizens-main:2.0.29-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.10'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'

    // Dependency injection
    implementation 'com.google.dagger:dagger:2.41'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.41'

    // Tools
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.spongepowered:configurate-yaml:4.1.2'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.3'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build.dependsOn tasks.shadowJar

shadowJar {
    relocate 'com.google.gson', 'net.motimaa.lib.com.google.gson'
    relocate 'dagger', 'net.motimaa.lib.dagger'
    relocate 'javax.inject', 'net.motimaa.lib.javax.inject'
    relocate 'org.spongepowered', 'net.motimaa.lib.org.spongepowered'
    relocate 'org.yaml', 'net.motimaa.lib.org.yaml'
    relocate 'io.leangen', 'net.motimaa.lib.io.leangen'
    relocate 'com.zaxxer', 'net.motimaa.lib.com.zaxxer'
    relocate 'org.mariadb.jdbc', 'net.motimaa.lib.org.mariadb.jdbc'

    destinationDirectory.set(file("$rootDir/builds/"))
    archiveBaseName.set('SkyblockCore')
    archiveClassifier.set('')
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
